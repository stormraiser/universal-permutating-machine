tag U = (1, 1, 1), D = (1, 0.8, 0), R = (0, 0, 0.8);
tag L = (0, 0.5, 0), F = (0.8, 0, 0), B = (0.8, 0.3, 0);
tag body = (0.1, 0.1, 0.1);

symmetry x {
    cycle (U, R, D, L);
    axisangle (1, 0, 0, -90);
}

symmetry y {
    cycle (F, U, B, D);
    axisangle (0, 1, 0, -90);
}

geometry center {
    model "block_3.off";
    translate(0, 0, 9.5)
        model "sticker_3.off";
}

geometry edge {
    model "block_3.off";
    translate(0, 0, 9.5)
        model "sticker_3.off";
    translate(0, 9.5, 0)
        rotate(-90, 0, 0)
            model "sticker_3.off";
}

geometry corner {
    model "block_3.off";
    translate(0, 0, 9.5)
        model "sticker_3.off";
    translate(0, 9.5, 0)
        rotate(-90, 0, 0)
            model "sticker_3.off";
    translate(9.5, 0, 0)
        rotate(0, 90, 0)
            model "sticker_3.off";
}

auto (x, y) {
    translate(0, 0, 19)
        block U = center(body, U);
    translate(0, 19, 19)
        block UR = edge(body, U, R);
    translate(19, 19, 19)
        block URF = corner(body, U, R, F);
    block alias (UR, RU), (URF, RFU);
    position U, UR, URF;
    
    operation U {
        axisangle(0, 0, 1, -90) {
            cycle (U);
            shift (UR, UF), (RU, FU), (URF, UFL), (RFU, FLU), (FUR, LUF);
        }
    }
    operation U' inverse U;
    
    operation Um {
        axisangle(0, 0, 1, -90)
            shift (R, F), (RF, FL), (FR, LF);
    }
    
    binding U {
        invoke U;
        translate(0, 0, 19)
            click "selector_3_1_cw.off";
    }
    
    binding U' {
        invoke U';
        translate(0, 0, 19)
            click "selector_3_1_ccw.off";
    }
    
    binding Um {
        invoke Um;
        click "selector_3_2_cw.off";
    }
}

operation dummy;

binding dummy {
    invoke dummy;
    click "selector_3_0.off";
}
